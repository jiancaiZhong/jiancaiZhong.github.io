---
layout: mypost
title: 解决谷歌翻译不可用的问题
categories: [Google]
---

> [项目托管地址](https://gist.github.com/bookfere) 脚本再管理员模式下运行

- Windows 保存为 **fix-google-translate-cn.bat**

  ```bash
  :: Copyright (c)2022 https://bookfere.com
  :: This is a batch script for fixing Google Translate and making it available
  :: in the Chinese mainland. If you experience any problem, visit the page below:
  :: https://bookfere.com/post/1020.html
  
  @echo off
  setlocal enabledelayedexpansion
  chcp 437 >NUL
  
  set "source_domain=google.cn"
  set "target_domain=translate.googleapis.com"
  set "hosts_file=C:\Windows\System32\drivers\etc\hosts"
  
  goto :start
  
  :generate_new_rule
      echo [1] Automatically [2] Manually
      set /p action="Enter a number to choose an IP adding method:"
  
      if "%action%"=="2" (
          set /p ip="Please enter a valid IP address: "
      )
  
      if "%ip%"=="" (
          for /f "skip=4 tokens=2" %%a in ('"nslookup %source_domain% 2>NUL"') do set ip=%%a
      )
  
      set "new_rule=%ip% %target_domain%"
  goto:EOF
  
  :start
  
  set "old_rule=null"
  set "comment=# Fix Google Translate CN"
  
  for /f "tokens=*" %%i in ('type %hosts_file%') do (
      set "line=%%i"
      :: Retrieve the rule If the target domain exists.
      if not "!line:%target_domain%=!"=="%%i" set "old_rule=%%i"
  )
  
  if not "%old_rule%"=="null" (
      echo A rule has been added to the hosts file.
      echo [1] Update [2] Delete
      set /p action="Enter a number to choose an action: "
      if "!action!"=="1" (
          call :generate_new_rule
          if not "%old_rule%"=="!new_rule!" (
              echo Deleting the rule "%old_rule%"
              echo Adding the rule "!new_rule!"
              set "new_line=false"
              for /f "tokens=*" %%i in ('type %hosts_file% ^| find /v /n "" ^& break ^> %hosts_file%') do (
                  set "rule=%%i"
                  set "rule=!rule:*]=!"
                  if "%old_rule%"=="!rule!" set "rule=!new_rule!"
                  if "!new_line!"=="true" >>%hosts_file% echo.
                  >>%hosts_file% <NUL set /p="!rule!"
                  set "new_line=true"
              )
          ) else (
              echo The rule already exists, nothing to do.
          )
      ) else (
          if "!action!"=="2" (
              echo Deleting the rule "%old_rule%"
              set "new_line=false"
              for /f "tokens=*" %%i in ('
                  type "%hosts_file%" ^| findstr /v /c:"%comment%" ^| findstr /v "%target_domain%" ^| find /v /n "" ^& break ^> "%hosts_file%"
              ') do (
                  set "line=%%i"
                  set "line=!line:*]=!"
                  if "!new_line!"=="true" >>%hosts_file% echo.
                  >>%hosts_file% <NUL set /p="!line!"
                  set "new_line=true"
              )
          )
      )
  ) else (
      call :generate_new_rule
      echo Adding the rule "!new_rule!"
      echo.>>%hosts_file%
      echo %comment%>>%hosts_file%
      <NUL set /p="!new_rule!">>%hosts_file%
  )
  
  echo Done.
  pause
  ```

- macOS保存为 **fix-google-translate-cn.sh**

  ```bash
  #!/bin/bash
  
  # Copyright (c)2022 https://bookfere.com
  # This is a batch script for fixing Google Translate and making it available
  # in the Chinese mainland. If you experience any problem, visit the page below:
  # https://bookfere.com/post/1020.html
  
  SOURCE_DOMAIN=google.cn
  TARGET_DOMAIN=translate.googleapis.com
  HOSTS_FILE=/etc/hosts
  
  HOST_CMD=/usr/bin/host
  CUT_CMD=/usr/bin/cut
  SED_CMD=/usr/bin/sed
  
  function generate_new_rule() {
      echo '[1] Automatically [2] Manually'
      echo -n 'Enter a number to choose an IP adding method: '
      read action
  
      if [ "$action" == '2' ]; then
          echo -n 'Please enter a valid IP address: '
          read IP
      fi
  
      if [ ! -n "$IP" ]; then
          IP=$($HOST_CMD -t A $SOURCE_DOMAIN | $CUT_CMD -d ' ' -f 4)
      fi
  
      NEW_RULE="$IP $TARGET_DOMAIN"
  }
  
  OLD_RULE=$(cat $HOSTS_FILE | grep $TARGET_DOMAIN)
  COMMENT="# Fix Google Translate CN"
  
  if [ -n "$OLD_RULE" ]; then
      echo 'A rule has been added to the hosts file. '
      echo '[1] Update [2] Delete'
      echo -n 'Enter a number to choose an action: '
      read action
      if [ "$action" == '1' ]; then
          generate_new_rule
          if [ "$OLD_RULE" != "$NEW_RULE" ]; then
              echo "Deleting the rule \"$OLD_RULE\""
              echo "Adding the rule \"$NEW_RULE\""
              $SED_CMD -i '' "s/.*${TARGET_DOMAIN}/${NEW_RULE}/" $HOSTS_FILE
          else
              echo 'The rule already exists, nothing to do.'
          fi
      elif [ "$action" == '2' ]; then
          echo "Deleting the rule \"$OLD_RULE\""
          PATTERN="s/\n\{0,\}${COMMENT}\n.* ${TARGET_DOMAIN}//"
          $SED_CMD -i '' -e ':a' -e 'N' -e '$!ba' -e "$PATTERN" $HOSTS_FILE
      fi
  else
      generate_new_rule
      echo "Adding the rule \"$NEW_RULE\""
      echo -ne "\n${COMMENT}\n${NEW_RULE}" >> $HOSTS_FILE
  fi
  
  echo 'Done.'
  ```

  